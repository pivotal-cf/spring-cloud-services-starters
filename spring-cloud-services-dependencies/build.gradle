/*
 * Copyright 2002-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-platform'
    id 'maven-publish'
}

description = "Spring Cloud Services Dependencies"

repositories {
    mavenCentral()
}

dependencies {
    constraints {
        api "io.pivotal.spring.cloud:spring-cloud-services-starter-circuit-breaker:${project.version}"
        api "io.pivotal.spring.cloud:spring-cloud-services-starter-config-client:${project.version}"
        api "io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry:${project.version}"

        api "org.springframework.cloud:spring-cloud-starter-netflix-hystrix:${springCloudStarterNetflixHystrixVersion}"
        api "org.springframework.cloud:spring-cloud-netflix-hystrix-stream:${springCloudStarterNetflixHystrixVersion}"
        api "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudStarterNetflixEurekaVersion}"

        api "io.pivotal.spring.cloud:spring-cloud-services-cloudfoundry-connector:${springCloudServicesConnectorsVersion}"
        api "io.pivotal.spring.cloud:spring-cloud-services-spring-connector:${springCloudServicesConnectorsVersion}"
        api "io.pivotal.spring.cloud:cloudfoundry-certificate-truster:${cloudfoundryCertificateTrusterVersion}"
    }
}

publishing {
    repositories {
        maven {
            url = project.properties.getOrDefault('publicationRepository', "${System.getenv('HOME')}/.m2/repository")
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform

            pom {
                afterEvaluate {
                    name = project.name
                    description = project.description
                }
                url = "https://projects.spring.io/spring-cloud"
                organization {
                    name = "Pivotal Software, Inc."
                    url = "https://www.spring.io"
                }
                licenses {
                    license {
                        name = "Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                    }
                }
                scm {
                    url = "https://github.com/pivotal-cf/spring-cloud-services-starters"
                    connection = "scm:git:https://github.com/pivotal-cf/spring-cloud-services-starters.git"
                    developerConnection = "scm:git:https://github.com/pivotal-cf/spring-cloud-services-starters.git"
                }
                developers {
                    developer {
                        id = "royclarkson"
                        name = "Roy Clarkson"
                        email = "rclarkson@pivotal.io"
                        organization = "Pivotal Software, Inc."
                        organizationUrl = "https://www.spring.io"
                    }
                }
            }
        }
    }
}

