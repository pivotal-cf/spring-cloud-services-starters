/*
 * Copyright 2002-2024 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath(libs.spring.dependency.management.plugin)
        classpath(libs.build.info.extractor.plugin)
        classpath(libs.spring.javaformat.plugin)
        classpath(libs.nohttp.plugin)
    }
}

plugins {
    alias(libs.plugins.nohttp)
}

checkstyle {
    configDirectory.set(file("${project.rootDir}/checkstyle/"))
}

configure(allprojects) {
    group = "io.pivotal.spring.cloud"

    apply plugin: "maven-publish"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "jacoco"
    apply plugin: libs.plugins.spring.dependency.management.get().pluginId

    ext {
        javadocLinks = [
                "https://docs.oracle.com/en/java/javase/17/docs/api",
                "https://docs.spring.io/spring-framework/docs/current/javadoc-api/"
        ] as String[]
    }

    repositories {
        mavenCentral()
        maven { url = "https://repo.spring.io/release" }
        if (version =~ /((-M|-RC)[0-9]+|-SNAPSHOT)$/) {
            maven { url = "https://repo.spring.io/milestone" }
        }
        if (version.endsWith('-SNAPSHOT')) {
            maven { url = "https://repo.spring.io/snapshot" }
        }
    }
}

subprojects {
    apply from: "${rootProject.projectDir}/publish-maven.gradle"
}

configure(javaProjects) {
    apply plugin: "java-library"
    apply plugin: "checkstyle"
    apply plugin: libs.plugins.spring.javaformat.get().pluginId

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        options.encoding = "UTF-8"
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        options.encoding = "UTF-8"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:all"]

    test {
        // enable JUnit 5
        useJUnitPlatform()

        testLogging {
            // display all the events
            events 'PASSED', 'FAILED', 'SKIPPED'
            // display stdout and stderr
            showStandardStreams = true
        }

        // create a summary after the execution
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }

        // print failed tests after the execution
        def failedTests = []

        afterTest { test, result ->
            if (result.resultType == TestResult.ResultType.FAILURE) {
                failedTests << test
            }
        }

        afterSuite {
            failedTests.each { test -> println "FAILED test: ${test.className} > ${test.name}" }
        }

        finalizedBy jacocoTestReport // report is always generated after tests run

        jacocoTestReport {
            dependsOn test // tests are required to run before generating the report

            reports {
                xml.required = true
                html.required = false
                csv.required = false
            }
        }
    }

    jar {
        manifest.attributes["Created-By"] =
                "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        manifest.attributes["Implementation-Title"] = project.name
        manifest.attributes["Implementation-Version"] = project.version

        from("${rootProject.projectDir}/src/dist") {
            include "license.txt"
            include "notice.txt"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }

    javadoc {
        description = "Generates project-level javadoc for use in -javadoc jar"

        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.links(project.ext.javadocLinks)
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    dependencies {
        testRuntimeOnly(libs.nohttp)
    }

    dependencyManagement {
        imports {
            mavenBom "${libs.spring.cloud.dependencies.get().module}:${libs.versions.spring.cloud.get()}"
            mavenBom "${libs.spring.boot.dependencies.get().module}:${libs.versions.spring.boot.get()}"
        }
    }

    checkstyle {
        configDirectory = rootProject.file("checkstyle")
        showViolations = true
    }

    tasks.named("checkstyleMain", Checkstyle) {
        source = fileTree("src/main/java")
    }

    tasks.named("checkstyleTest", Checkstyle) {
        source = fileTree("src/test/java")
        configFile = configDirectory.get().file("checkstyle-test.xml").asFile
    }

    tasks.register('allDependencyInsight', DependencyInsightReportTask) {}
    tasks.register('allDependencies', DependencyReportTask) {}
}

configure(rootProject) {
    description = "Spring Cloud Services Starter Build"

    tasks.register('api', Javadoc) {
        group = "Documentation"
        description = "Generates aggregated Javadoc API documentation."
        title = "${rootProject.description} ${version} API"
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = rootProject.description
        options.links(project.ext.javadocLinks)
        options.addStringOption('Xdoclint:none', '-quiet')

        source projectsWithSource.collect { project ->
            project.sourceSets.main.allJava
        }

        classpath = files(projectsWithSource.collect { project ->
            project.sourceSets.main.compileClasspath
        })

        maxMemory = "1024m"
        destinationDir = layout.buildDirectory.dir("api").get().asFile
    }

}

configure(subprojects) {
    dependencyManagement {
        dependencies {
            dependency "io.pivotal.cfenv:java-cfenv-boot-pivotal-scs:${libs.versions.java.cfenv.get()}"
            dependency "io.pivotal.cfenv:java-cfenv:${libs.versions.java.cfenv.get()}"
        }
    }
}

allprojects {
    tasks.withType(Test).forEach { Test task ->
        task.with {
            testLogging {
                exceptionFormat = "full"
                events = ["passed", "skipped", "failed"]
                showStandardStreams = false
            }

            StringBuilder testOutput = new StringBuilder()

            beforeTest {
                testOutput.setLength(0)
            }

            onOutput { descriptor, TestOutputEvent event ->
                testOutput.append(event.message)
            }

            afterTest { TestDescriptor descriptor, TestResult result ->
                if (result.resultType == TestResult.ResultType.FAILURE && testOutput.size() > 0) {
                    logger.lifecycle("\n\n${testOutput}")
                }
            }
        }
    }
}

def getProjectsWithSource() {
    [
            project(":spring-cloud-services-config-client-autoconfigure"),
            project(":spring-cloud-services-service-registry-autoconfigure")
    ]
}

def getJavaProjects() {
    subprojects - project(":spring-cloud-services-dependencies")
}
